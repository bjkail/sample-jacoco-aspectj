apply plugin: 'java'
apply plugin: 'jacoco'

configurations {
    ajc
    aspects
    compile {
        extendsFrom aspects
    }
}

dependencies {
    aspects 'io.astefanutti.metrics.aspectj:metrics-aspectj:1.2.0'
    compile 'io.astefanutti.metrics.aspectj:metrics-aspectj-deps:1.2.0'
    ajc 'org.aspectj:aspectjtools:1.8.10'
    testCompile 'junit:junit:4.12'
}

def aspectj = {destDir, aspectPath, inpath, classpath ->
    ant.taskdef(resource: 'org/aspectj/tools/ant/taskdefs/aspectjTaskdefs.properties',
            classpath: configurations.ajc.asPath)

    ant.iajc(
            fork: 'false',
            showWeaveInfo: 'true',
            verbose: 'true',
            source: project.sourceCompatibility,
            target: project.targetCompatibility,
            failonerror: 'true',
            destDir: destDir,
            aspectPath: aspectPath,
            inpath: inpath,
            classpath: classpath,
    )
}

compileJava {
    doLast {
        aspectj project.sourceSets.main.output.classesDir.absolutePath,
                configurations.aspects.asPath,
                project.sourceSets.main.output.classesDir.absolutePath,
                project.sourceSets.main.runtimeClasspath.asPath
    }
}
